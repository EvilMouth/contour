apply plugin: 'com.github.ben-manes.versions'

buildscript {
  ext.versions = [
    'minSdk': 21,
    'targetSdk': 30,
    'agp': '7.0.0',
    'lint': '30.0.0',  // = 23.0.0 + agp
    'kotlin': '1.5.10',
    'autoService': '1.0',
  ]

  ext.deps = [
    'androidx': [
      'appCompat': 'androidx.appcompat:appcompat:1.3.1',
      'ktx': 'androidx.core:core-ktx:1.6.0',
      'transition': 'androidx.transition:transition:1.4.1',
    ],
    'picasso': 'com.squareup.picasso:picasso:2.8',
    'lint': [
      'api': "com.android.tools.lint:lint-api:${versions.lint}",
      'lint': "com.android.tools.lint:lint:${versions.lint}",
      'tests': "com.android.tools.lint:lint-tests:${versions.lint}",
    ],
    'auto': [
      'service': "com.google.auto.service:auto-service:${versions.autoService}",
      'serviceAnnotations': "com.google.auto.service:auto-service-annotations:${versions.autoService}",
    ],
    'junit': 'junit:junit:4.13.2',
    'robolectric': 'org.robolectric:robolectric:4.6.1',
    'truth': 'com.google.truth:truth:1.1.3',
    'plugins': [
      'agp': "com.android.tools.build:gradle:${versions.agp}",
      'kotlin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    ],
  ]

  dependencies {
    classpath deps.plugins.agp
    classpath deps.plugins.kotlin
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.39.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.17.0'
  }

  repositories {
    google()
    mavenCentral()
    gradlePluginPortal()
  }
}

subprojects {
  repositories {
    google()
    mavenCentral()
  }

  tasks.withType(JavaCompile).configureEach { task ->
    task.sourceCompatibility = JavaVersion.VERSION_1_8
    task.targetCompatibility = JavaVersion.VERSION_1_8
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
    task.kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

subprojects { project ->
  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
      jvmTarget = "1.8"
    }
  }
}

tasks.wrapper {
  distributionType = Wrapper.DistributionType.BIN
}